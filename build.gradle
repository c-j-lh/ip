plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'checkstyle'
}

repositories { mavenCentral() }

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// define the JavaFX version ONCE
def javafxVersion = '17.0.7'

dependencies {
    // JavaFX (Windows natives only)
    implementation "org.openjfx:javafx-base:${javafxVersion}:win"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:win"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:win"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:win"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
    testRuntimeOnly  "org.junit.jupiter:junit-jupiter-engine:5.10.0"
}

test { useJUnitPlatform() }

application {
    // GUI entry point (package-qualified)
    mainClass.set("ramarama.Launcher")
}

tasks.register('runGui', JavaExec) {
    group = 'application'
    description = 'Run JavaFX GUI'
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set('ramarama.Launcher')
    standardInput = System.in
}

shadowJar {
    archiveBaseName = "ramarama"
    archiveClassifier = null
}

run { 
    standardInput = System.in 
    enableAssertions = true
}

checkstyle { toolVersion = '11.0.0' }
